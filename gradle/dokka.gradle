/*
 * Check usage information at: http://hexagonkt.com/gradle/#dokka
 */

apply(plugin: "kotlin")
apply(plugin: "maven-publish")
apply(plugin: "org.jetbrains.dokka")

dokka {
    configuration {
        reportUndocumented = false
        includes = filesCollection(projectDir, "*.md")
        samples = filesCollection("$projectDir/src/test/kotlin", "**/*SamplesTest.kt")
        sourceRoot { path = "$projectDir/src/main/kotlin" }
        sourceRoot { path = "$projectDir/src/test/kotlin" }
    }
}

task("dokkaJar", type: Jar, dependsOn: "dokka") {
    archiveClassifier.set("javadoc")
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(fileTree("$buildDir/dokka"))
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(dokkaJar)
        }
    }
}

private List<String> filesCollection(final Object dir, final String pattern) {
    return fileTree(dir: dir, include: pattern).getFiles().toList().collect { it.absolutePath }
}
